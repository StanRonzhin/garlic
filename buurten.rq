#+ summary: CBS data for buurten
#+ tags: 
#+   - buurtNaam
#+ enumerate:		
#+   - woonplaatsNaam		

prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://data.pdok.nl/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?buurtNaam ?cafeBuurt ?wkt {
  service <https://data.pdok.nl/sparql> {
    bind ("Apeldoorn"@nl as ?_woonplaatsNaam_nl)
    ?registratiefGebied brt:naamOfficieel ?_woonplaatsNaam_nl ;
                        brt:nummer ?registratiefGebiedCode
  }
  graph <https://data.pdok.nl/cbs/2015> {
    bind (concat("GM",?registratiefGebiedCode) as ?gemeenteCode)
    ?buurt cbs:GM_CODE ?gemeenteCode ;
           cbs:BU_CODE ?buurtCode ;
           cbs:WK_CODE ?wijkCode ;
           geo:hasGeometry/geo:asWKT ?wkt ;
           cbs:AF_CAFE ?cafeBuurt ;
           cbs:BU_NAAM ?buurtNaam .
  }
}
limit 100
